configure(allprojects) { project ->

	repositories {
		mavenLocal()
		mavenCentral()
	}

	group 	= "com.willydupreez.aphorism"
	version = "0.0.1-SNAPSHOT"

	ext.commonsBeanUtilsVersion			= "1.8.3"
	ext.commonsDaemonVersion			= "1.0.15"

	ext.gradleVersion					= "2.0"
	ext.jacksonVersion					= "2.4.1"
	ext.jettyVersion					= "9.2.2.v20140723"
	ext.junitVersion					= "4.11"
	ext.log4jVersion					= "2.0"

	ext.commonsDbcp2Version				= "2.0.1"
	ext.mysqlConnectorJavaVersion		= "5.1.32"
	ext.postgresqlVersion				= "9.3-1102-jdbc41"
	ext.h2databaseVersion				= "1.4.181"
	ext.hsqldbVersion					= "2.3.2"

	ext.servletVersion					= "3.1.0"
	ext.undertowVersion					= "1.1.0.Beta6"

	apply plugin: "java"
	apply plugin: "maven"
	apply plugin: "eclipse"

	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
	}

	compileJava {
		sourceCompatibility=1.8
		targetCompatibility=1.8
	}

	compileTestJava {
		sourceCompatibility=1.8
		targetCompatibility=1.8
	}

	dependencies {
		testCompile("junit:junit:${junitVersion}")
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

}

project("aphorism-app") {
	description = "Utilities"

	dependencies {
		compile(project(":aphorism-util"))
		compile(project(":aphorism-daemon"))
	}
}

project("aphorism-config") {
	description = "Utilities"

	dependencies {
		compile(project(":aphorism-util"))
	}
}

project("aphorism-daemon") {
	description = "Application Daemon"

	dependencies {
		compile(project(":aphorism-config"))
	}
}

project("aphorism-db") {
	description = "Database"

	dependencies {
		compile("org.apache.commons:commons-dbcp2:${commonsDbcp2Version}")
		compile("mysql:mysql-connector-java:${mysqlConnectorJavaVersion}")
		compile("org.postgresql:postgresql:${postgresqlVersion}")
		compile("com.h2database:h2:${h2databaseVersion}")
		compile("org.hsqldb:hsqldb:${hsqldbVersion}")
	}
}

project("aphorism-gradle-app-plugin") {
	description = "Aphorism Application - Gradle Plugin"

	apply plugin: "groovy"

	repositories {
		maven {
			url "http://repo.gradle.org/gradle/libs-releases-local"
		}
	}

	dependencies {
		compile gradleApi()
		compile localGroovy()
		compile("org.gradle:gradle-core:${gradleVersion}")
		compile("org.gradle:gradle-tooling-api:${gradleVersion}")
	}
}

project("aphorism-hal") {
	description = "JSON HAL (Hypertext Application Language)"

	dependencies {
		compile(project(":aphorism-util"))

		compile("commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}")

		compile("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}")
		compile("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")
		compile("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
	}
}

project("aphorism-logging") {
	description = "Application Logging Utilities"

	dependencies {
		compile("org.apache.logging.log4j:log4j-api:${log4jVersion}")
		compile("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	}
}

project("aphorism-server") {
	description = "Server Capabilities"

	dependencies {
		compile("io.undertow:undertow-core:${undertowVersion}")
		compile("org.eclipse.jetty:jetty-server:${jettyVersion}") {
			exclude module: "javax.servlet-api"
		}
		compile("javax.servlet:javax.servlet-api:${servletVersion}")
	}
}

project("aphorism-util") {
	description = "Utilities"
}

task wrapper(type: Wrapper) {
	gradleVersion = "2.0"
}
